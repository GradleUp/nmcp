import kotlin.test.assertEquals
import kotlin.test.assertNull
import nmcp.NmcpAggregationExtension

plugins {
    id("base")
    alias(libs.plugins.kgp).apply(false)
}

buildscript {
    dependencies {
        classpath("com.gradleup.nmcp:nmcp")
        classpath("org.jetbrains.kotlin:kotlin-test:2.0.0")
    }
}

apply(plugin = "com.gradleup.nmcp.aggregation")

group = "net.mbonnin.tnmcp"
version = "0.0.3"

subprojects {
    val project = this
    val projectName = project.name

    pluginManager.apply("org.jetbrains.kotlin.jvm")
    pluginManager.apply("maven-publish")
    pluginManager.apply("signing")

    val publishing = project.extensions.getByType(PublishingExtension::class.java)

    publishing.publications {
        create("default", MavenPublication::class.java) {
            from(project.components.findByName("java"))
            artifact(
                tasks.register("emptySources", Jar::class.java) {
                    archiveClassifier = "sources"
                },
            )
            artifact(
                tasks.register("emptyDocs", Jar::class.java) {
                    archiveClassifier = "javadoc"
                },
            )

            groupId = project.rootProject.group.toString()
            version = project.rootProject.version.toString()
            artifactId = projectName

            pom {
                name.set(projectName)
                packaging = "jar"
                description.set(projectName)
                url.set("https://github.com/martinbonnin/test-nmcp")

                scm {
                    url.set("https://github.com/martinbonnin/test-nmcp")
                    connection.set("https://github.com/martinbonnin/test-nmcp")
                    developerConnection.set("https://github.com/martinbonnin/test-nmcp")
                }

                licenses {
                    license {
                        name.set("MIT")
                    }
                }

                developers {
                    developer {
                        id.set("mbonnin")
                        name.set("mbonnin")
                    }
                }
            }
        }
    }

    project.extensions.getByType(SigningExtension::class.java).apply {
        sign(publishing.publications)
        useInMemoryPgpKeys(
            System.getenv("GPG_PRIVATE_KEY"),
            System.getenv("GPG_PRIVATE_KEY_PASSWORD")
        )
    }

    tasks.withType(Sign::class.java) {
        isEnabled = System.getenv("GPG_PRIVATE_KEY").isNullOrBlank().not()
    }
}

extensions.getByType<NmcpAggregationExtension>().apply {
    publishAllProjectsProbablyBreakingProjectIsolation()

    centralPortal {
        username = System.getenv("MAVEN_CENTRAL_USERNAME")
        password = System.getenv("MAVEN_CENTRAL_PASSWORD")
        publishingType = "USER_MANAGED"
    }
}

/**
 * A task that checks that the zip file generated by the aggregation plugin contains the expected files.
 *
 * This task assumes that no signing key is present (GPG_PRIVATE_KEY must be empty). If it fails, double check your environment variables.
 */
val checkZip = tasks.register("checkZip") {
    inputs.file(tasks.named("nmcpZipAggregation").flatMap { (it as Zip).archiveFile })

    doLast {
        val paths = mutableListOf<String>()
        zipTree(inputs.files.singleFile).visit {
            paths.add(this.path)
        }
        check(System.getenv("GPG_PRIVATE_KEY") == null) {
            "checkZip doesn't working if signing is enabled"
        }
//        println(paths.sorted().joinToString(",\n"))
        assertEquals(
            listOf(
                "net",
                "net/mbonnin",
                "net/mbonnin/tnmcp",
                "net/mbonnin/tnmcp/module1",
                "net/mbonnin/tnmcp/module1/${project.version}",
                "net/mbonnin/tnmcp/module1/${project.version}/module1-${project.version}-javadoc.jar",
                "net/mbonnin/tnmcp/module1/${project.version}/module1-${project.version}-javadoc.jar.md5",
                "net/mbonnin/tnmcp/module1/${project.version}/module1-${project.version}-javadoc.jar.sha1",
                "net/mbonnin/tnmcp/module1/${project.version}/module1-${project.version}-javadoc.jar.sha256",
                "net/mbonnin/tnmcp/module1/${project.version}/module1-${project.version}-javadoc.jar.sha512",
                "net/mbonnin/tnmcp/module1/${project.version}/module1-${project.version}-sources.jar",
                "net/mbonnin/tnmcp/module1/${project.version}/module1-${project.version}-sources.jar.md5",
                "net/mbonnin/tnmcp/module1/${project.version}/module1-${project.version}-sources.jar.sha1",
                "net/mbonnin/tnmcp/module1/${project.version}/module1-${project.version}-sources.jar.sha256",
                "net/mbonnin/tnmcp/module1/${project.version}/module1-${project.version}-sources.jar.sha512",
                "net/mbonnin/tnmcp/module1/${project.version}/module1-${project.version}.jar",
                "net/mbonnin/tnmcp/module1/${project.version}/module1-${project.version}.jar.md5",
                "net/mbonnin/tnmcp/module1/${project.version}/module1-${project.version}.jar.sha1",
                "net/mbonnin/tnmcp/module1/${project.version}/module1-${project.version}.jar.sha256",
                "net/mbonnin/tnmcp/module1/${project.version}/module1-${project.version}.jar.sha512",
                "net/mbonnin/tnmcp/module1/${project.version}/module1-${project.version}.module",
                "net/mbonnin/tnmcp/module1/${project.version}/module1-${project.version}.module.md5",
                "net/mbonnin/tnmcp/module1/${project.version}/module1-${project.version}.module.sha1",
                "net/mbonnin/tnmcp/module1/${project.version}/module1-${project.version}.module.sha256",
                "net/mbonnin/tnmcp/module1/${project.version}/module1-${project.version}.module.sha512",
                "net/mbonnin/tnmcp/module1/${project.version}/module1-${project.version}.pom",
                "net/mbonnin/tnmcp/module1/${project.version}/module1-${project.version}.pom.md5",
                "net/mbonnin/tnmcp/module1/${project.version}/module1-${project.version}.pom.sha1",
                "net/mbonnin/tnmcp/module1/${project.version}/module1-${project.version}.pom.sha256",
                "net/mbonnin/tnmcp/module1/${project.version}/module1-${project.version}.pom.sha512",
                "net/mbonnin/tnmcp/module2",
                "net/mbonnin/tnmcp/module2/${project.version}",
                "net/mbonnin/tnmcp/module2/${project.version}/module2-${project.version}-javadoc.jar",
                "net/mbonnin/tnmcp/module2/${project.version}/module2-${project.version}-javadoc.jar.md5",
                "net/mbonnin/tnmcp/module2/${project.version}/module2-${project.version}-javadoc.jar.sha1",
                "net/mbonnin/tnmcp/module2/${project.version}/module2-${project.version}-javadoc.jar.sha256",
                "net/mbonnin/tnmcp/module2/${project.version}/module2-${project.version}-javadoc.jar.sha512",
                "net/mbonnin/tnmcp/module2/${project.version}/module2-${project.version}-sources.jar",
                "net/mbonnin/tnmcp/module2/${project.version}/module2-${project.version}-sources.jar.md5",
                "net/mbonnin/tnmcp/module2/${project.version}/module2-${project.version}-sources.jar.sha1",
                "net/mbonnin/tnmcp/module2/${project.version}/module2-${project.version}-sources.jar.sha256",
                "net/mbonnin/tnmcp/module2/${project.version}/module2-${project.version}-sources.jar.sha512",
                "net/mbonnin/tnmcp/module2/${project.version}/module2-${project.version}.jar",
                "net/mbonnin/tnmcp/module2/${project.version}/module2-${project.version}.jar.md5",
                "net/mbonnin/tnmcp/module2/${project.version}/module2-${project.version}.jar.sha1",
                "net/mbonnin/tnmcp/module2/${project.version}/module2-${project.version}.jar.sha256",
                "net/mbonnin/tnmcp/module2/${project.version}/module2-${project.version}.jar.sha512",
                "net/mbonnin/tnmcp/module2/${project.version}/module2-${project.version}.module",
                "net/mbonnin/tnmcp/module2/${project.version}/module2-${project.version}.module.md5",
                "net/mbonnin/tnmcp/module2/${project.version}/module2-${project.version}.module.sha1",
                "net/mbonnin/tnmcp/module2/${project.version}/module2-${project.version}.module.sha256",
                "net/mbonnin/tnmcp/module2/${project.version}/module2-${project.version}.module.sha512",
                "net/mbonnin/tnmcp/module2/${project.version}/module2-${project.version}.pom",
                "net/mbonnin/tnmcp/module2/${project.version}/module2-${project.version}.pom.md5",
                "net/mbonnin/tnmcp/module2/${project.version}/module2-${project.version}.pom.sha1",
                "net/mbonnin/tnmcp/module2/${project.version}/module2-${project.version}.pom.sha256",
                "net/mbonnin/tnmcp/module2/${project.version}/module2-${project.version}.pom.sha512",
            ),
            paths.sorted()
        )
    }
}

tasks.named("build") {
    dependsOn(checkZip)
}
